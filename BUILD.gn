config("freetype_config") {
  include_dirs = [
    "include",
  ]
}

#TODO(mikejurka): Remove once we've migrated to the freetype2 target everywhere
group("freetype") {
  public_deps = [
    ":freetype2",
  ]
}

source_set("freetype2") {
  sources = [
    # The following files are not sorted alphabetically, but in the
    # same order as in Android.mk to ease maintenance.
    "src/base/ftbbox.c",
    "src/base/ftbitmap.c",
    "src/base/ftfntfmt.c",
    "src/base/ftfstype.c",
    "src/base/ftglyph.c",
    "src/base/ftlcdfil.c",
    "src/base/ftstroke.c",
    "src/base/fttype1.c",
    "src/base/ftbase.c",
    "src/base/ftsystem.c",
    "src/base/ftinit.c",
    "src/base/ftgasp.c",
    "src/base/ftmm.c",
    "src/gzip/ftgzip.c",
    "src/raster/raster.c",
    "src/sfnt/sfnt.c",
    "src/smooth/smooth.c",
    "src/autofit/autofit.c",
    "src/truetype/truetype.c",
    "src/cff/cff.c",
    "src/psnames/psnames.c",
    "src/pshinter/pshinter.c",
  ]

  defines = [
    "FT2_BUILD_LIBRARY",
    "DARWIN_NO_CARBON",
    # Long directory name to avoid accidentally using wrong headers.
    "FT_CONFIG_MODULES_H=<freetype-fuchsia-config/ftmodule.h>",
    "FT_CONFIG_OPTIONS_H=<freetype-fuchsia-config/ftoption.h>",
  ]

  public_configs = [ ":freetype_config" ]

  deps = [
    "//third_party/libpng",
    "//third_party/zlib",
  ]
}
